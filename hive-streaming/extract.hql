-- extract.hql 
-- This file captures the hive query generated by extract_paths() 
-- in AggregateMicroPath.py, for the ais_small_final data. 
-- Put here for debugging this single step.  

-- Setup
set mapred.reduce.tasks=96; set mapred.map.tasks=96;
set hive.server2.logging.operation.level=EXECUTION;
ADD FILES conf/config.py scripts/extract_path_segments.py conf/ais.ini;
;

-- Select * from AIS table
FROM(
    SELECT mmsi, dt, latitude, longitude
    FROM amp_data.ais_small_final
    DISTRIBUTE BY mmsi
    SORT BY mmsi, dt asc
) map_out

-- Transform with Python UDF and write "extract" table
INSERT OVERWRITE TABLE amp_data.micro_path_track_extract_ais_small_final
SELECT TRANSFORM ( 
    map_out.mmsi, map_out.dt, 
    map_out.latitude, map_out.longitude )
USING "python extract_path_segments.py conf/ais.ini"
AS id,alat,blat,alon,blon,adt,bdt,time,distance,velocity
; 